      PROGRAM EXAMPL3
      implicit double precision(a-h,o-z)
      DIMENSION S(4),G1(10),G2(10),B(10,10),C(10),MS1(20)
      DIMENSION VLB(4),VUB(4),SCAL(4)
      COMMON /VARABLE/ AOBJ,X(4),G(10)
      COMMON/GRAD/ ISC(10),IC(10),DF(4),A(4,10)
      COMMON/CONSAV/ RNUM(50),INUM(25)
      COMMON /CNMN1/ DELFUN,DABFUN,FDCH,FDCHM,CT,CTMIN,CTL,CTLMIN,
     .               ALPHAX,ABOBJ1,THETA,OBJ,NDV,NCON,NSIDE,IPRINT,
     .               NFDG,NSCAL,LINOBJ,ITMAX,ITRM,ICNDIR,IGOTO,NAC,
     .               INFO,INFOG,ITER
      COMMON /ANDATA/ LOOPCNT
      NAMELIST /CONPAR/ INFOG,INFO,NFDG,IPRINT,NDV,ITMAX,NCON,NSIDE,
     .                  ICNDIR,NSCAL,FDCH,FDCHM,CT,CTMIN,CTLMIN,THETA,
     .                  PHI,DELFUN,DABFUN,LINOBJ,ITRM,X,VLB,VUB,
     .                  N1,N2,N3,N4,N5,ALPHAX,ABOBJ1,CTL,ISC,SCAL
C
C     THIS PROGRAM EXECUTES THE EXAMPLE PROBLEM THREE OF THE CONMIN MANUAL.
C
C
C  INITIALIZE
C
      INFOG=0
      INFO=0
      NFDG=2
      IPRINT=1
      NDV=2
      ITMAX=40
      NCON=6
      NSIDE=1
      ICNDIR=0
      NSCAL=0
      FDCH=0.0
      FDCHM=0.0
      CT=0.0
      CTMIN=0.0
      CTL=0.0
      CTLMIN=0.0
      THETA=0.0
      PHI=0.0
      DELFUN=0.0
      DABFUN=0.0
      LINOBJ=1
      ITRM=0
      N1=4
      N2=10
      N3=10
      N4=10
      N5=20
      ALPHAX=0.0
      ABOBJ1=0.0
      CTL=0.0
      DO 5 I=1,NDV
        X(I)=1.0
        VLB(I)=0.001
        VUB(I)= 1.0E+10
    5 CONTINUE
C
      DO 6 J=1,NCON
        ISC(J)=0
    6 CONTINUE
C
C     READ THE PARAMETERS FOR CONMIN
C
CCC   READ(5,CONPAR)      USE DEFAULT VALUES
      WRITE(6,CONPAR)
      NLIM=ITMAX*(NDV+5)
C
C     NON-ITERATIVE PART OF ANALYSIS
C
      IGOTO = 0
C
C     ITERATIVE PART OF ANALYSIS
C
      DO 1000 I = 1,NLIM
        LOOPCNT=I
C
C       CALL THE OPTIMIZATION ROUTINE CONMIN
C
        CALL CONMIN(X,VLB,VUB,G,SCAL,DF,A,S,G1,G2,B,C,ISC,IC,MS1,N1,N2,
     .              N3,N4,N5)
C
        IF(IGOTO.EQ.0) LOOPCNT=-999
C
C       ANALYSIS MODULE
C
        CALL ANALYS
        OBJ=AOBJ
        IF (IGOTO.EQ.0) GO TO 1100
 1000 CONTINUE
C
C
 1100 CONTINUE
      CLOSE(6)
      STOP
      END


      SUBROUTINE ANALYS
      implicit double precision(a-h,o-z)
      COMMON /VARABLE/ AOBJ,X(4),G(10)
      COMMON/GRAD/ ISC(10),IC(10),DF(4),A(4,10)
      COMMON /CNMN1/ DELFUN,DABFUN,FDCH,FDCHM,CT,CTMIN,CTL,CTLMIN,
     .               ALPHAX,ABOBJ1,THETA,OBJ,NDV,NCON,NSIDE,IPRINT,
     .               NFDG,NSCAL,LINOBJ,ITMAX,ITRM,ICNDIR,IGOTO,NAC,
     .               INFO,INFOG,ITER
C
C   ROUTINE TO CALCULATE OBJECTIVE FUNCTION AND
C   CONSTRAINT  VALUES FOR OPTIMIZATION OF CONSTRAINED ROSEN-SUZUKI
C   FUNCTION.
C
C
      RHO= 0.1
      A1= X(1)
      A2= X(2)
C
      IF(INFO.GE.2) GO TO 10
C
C  OBJECTIVE FUNCTION
C
      AOBJ= 10.*RHO*(2.*SQRT(2.)*A1+A2)
C
C
C   CONSTRAINT VALUES
C
      DENOM= 2.*A1*A2 + SQRT(2.)*A1*A1
      SIG11= 20.*(SQRT(2.)*A1+A2)/DENOM
      SIG21= 20.*SQRT(2.)*A1/DENOM
      SIG31= -20.*A2/DENOM
C
      G(1)= -SIG11/15.-1.
      G(2)= SIG11/20.-1.
      G(3)= -SIG21/15.-1.
      G(4)= SIG21/20.-1.
      G(5)= -SIG31/15.-1.
      G(6)= SIG31/20.-1.
C
      GO TO 999
   10 CONTINUE
C
C
C    GRADIENT INFORMATION
C
      DF(1)=20.*SQRT(2.)*RHO
      DF(2)=10.*RHO
C
C  GRADIENTS OF ACTIVE AND VIOLATED CONSTRAINTS
C  WILL BE CALCULATED BY FINITE DIFFERENCE WITHIN
C  CONMIN
C
C
  999 RETURN
      END
